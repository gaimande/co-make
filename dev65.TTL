; *****************************************************
; *   Auto halt the making process when any error occur
; *   Version 1.0
; *   Author: quyenlv@dasannetworks.com
; *   Date: September 20th 2014
; *****************************************************
include 'boxConfig.ttl'

; Hide MACRO dialog box
;show -1

:login
filesearch auth_file
if result=1 then
    fileopen fhandle auth_file 0
    filereadln fhandle _constr
    fileclose fhandle
else
    goto _end
endif

:connect
; Openning SSH conection
connect _constr
if result != 2 then
    yesnobox 'Connection fail. Try again?' 'Dev Caution'
    if result then
        goto login
    else
        goto _end
    endif
endif

; Entering synchronous communication mode (useful in most cases)
setsync 1

; Assigning new title to TeraTerm window
settitle 'Dev65'
setmulticastname 'Dev65'

; Set the shell prompt as an easy clue to detect
pause 10
call set_prompt

:main_loop
; Send SOS message to restart the TTY_BOX
sendmulticast 'TTY_BOX' sos_msg 13 10

; Wait for user enter 'make' command
waitln 'make'#13#10
mpause 500
sendmulticast 'TTY_BOX' ack_msg 13 10

; Send status to TI Controller
exec 'cmd /c echo "RUNNING" > COM5'

; Wail till finish or any errors occur
wait sBoxPrompt 'Error 1' #3

;Finish make process. Need to check whether success or not
if result=1 then
    call check_result
    if ret=0 then
        call display_success
    else
        call display_error
    endif
elseif result=2 then
    call display_error
elseif result=3 then
    ; Send CTRL+C to halt the making process
	sendln #3
	call display_error
endif

mpause 200
sendmulticast 'TTY_BOX' done_msg 13 10

; Loop for the new make process
goto main_loop

; *****************************************************
; *
; *   Function        : set_prompt
; *   Description     : Set prompt for build server
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:set_prompt

sprintf2 prompt "PS1='\[\e[1;32m\]%s ~$ '" sBoxPrompt
sendln prompt 13 10

return



; *****************************************************
; *
; *   Function        : check_result
; *   Description     : Check the make result after making
; *   Input           : None
; *   Output          : ret (-1: error, 0: OK)
; *
; *****************************************************
:check_result

sendln 'echo $?'

flushrecv
waitn 10

strmatch inputstr '0'
if result=0 then
    ret = -1
else
    ret = 0
endif

return



; *****************************************************
; *
; *   Function        : display_error
; *   Description     : Display error pop-up window
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:display_error

exec 'cmd /c start %comspec% /c "mode 40,10&title Making Status&color 4e&echo.&echo. Build Failed.&echo. Please check the output log!&echo.&echo. Press any keys to exit!&pause>NUL"'

; Send status to TTY Controller
sendmulticast 'TTY_BOX' 'ERROR' 13 10

return



; *****************************************************
; *
; *   Function        : display_success
; *   Description     : Display success pop-up window
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:display_success

exec 'cmd /c start %comspec% /c "mode 40,10&title Making Status&color 2e&echo.&echo. Build Successfully.&echo.&echo. Press any keys to exit!&pause>NUL"'

; Send status to TTY Controller
sendmulticast 'TTY_BOX' 'SUCCESS' 13 10

return



:_end
disconnect
closett
end