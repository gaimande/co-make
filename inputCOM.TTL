; *****************************************************
; *   Auto halt the making process when any error occur
; *   Version 1.0
; *   Author: quyenlv@dasannetworks.com
; *   Date: September 20th 2014
; *****************************************************
include 'boxConfig.ttl'

; Hide MACRO dialogue box
;show -1

; Return value with 0-Configure Fail and 1-Configure OK
ret = 0

:login_tty
; Calling Popup window to get host address
inputbox 'Enter COM port number' 'Connect to serial port'
_comport=inputstr
; Check NULL
strcompare _comport ""
if result=0 goto login_tty

; Check numeric
str2int val _comport
if result=0 goto login_tty

; Building connection string
sprintf2 _constr_tty '/C=%s /BAUD=9600' _comport

; Openning serial conection
connect _constr_tty
if result != 2 then
    closett
    yesnobox 'Connection fail. Try again?' 'Caution'
    if result then
        goto login_tty
    else
        end
    endif
endif

fileopen fhandle com_file 0
filewrite fhandle _constr_tty
fileclose fhandle

ret = 1

closett