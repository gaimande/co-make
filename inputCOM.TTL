; *****************************************************
; *   Inputting COM connection information
; *   Version 1.0
; *   Email: gaimande@gmail.com
; *   Date: September 20th 2014
; *****************************************************
include 'config.ttl'

; Hide MACRO dialogue box
show -1

; Return value with 0-Configure Fail and 1-Configure OK
ret = 0

:login_tty
; Calling Popup window to get host address
inputbox 'Enter COM port number' 'Connect to serial port'
_comport=inputstr
; Check NULL
strcompare _comport ""
if result=0 goto login_tty

; Check numeric
str2int val _comport
if result=0 goto login_tty

; Calling Popup window to get baudrate
:_get_baudrate
inputbox 'Enter COM baud rate' 'Connect to serial port'
_baudrate=inputstr
; Check NULL
strcompare _baudrate ""
if result=0 goto _get_baudrate

; Check numeric
str2int val _baudrate
if result=0 goto _get_baudrate

; Building connection string
sprintf2 _constr_tty '/C=%s /BAUD=%s' _comport _baudrate

; Openning serial conection
connect _constr_tty
if result != 2 then
    closett
    yesnobox 'Connection fail. Try again?' 'Caution'
    if result then
        goto login_tty
    else
        end
    endif
endif

; Save COM connection information
fileopen fhandle conf_file 0 1
filestrseek fhandle '[COM]'#13#10
fileclose fhandle

if result=0 then
    fileopen fhandle conf_file 1
    filewriteln fhandle ''
    filewriteln fhandle '[COM]'
else
    fileopen fhandle conf_file 0
    filestrseek fhandle '[COM]'#13#10
endif

sprintf2 _buff 'port=%s' _comport
filewriteln fhandle _buff
sprintf2 _buff 'baud=%s' _baudrate
filewriteln fhandle _buff
fileclose fhandle

ret = 1

closett