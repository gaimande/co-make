; *****************************************************
; *   COM connection controller
; *   Version 1.0
; *   Email: gaimande@gmail.com
; *   Date: September 20th 2014
; *****************************************************
include 'config.ttl'

; Hide MACRO.
show -1

; Hide Tera Term.
showtt -1

:connect
call get_com_info
if ret=0 then
    yesnobox 'Cannot get COM connection information. Input again?' 'TTY Caution'
    if result then
        include 'inputcom.ttl'
        if ret=0 goto end
        goto connect
    else
        end
    endif
endif

; Building connection string
sprintf2 _constr '/C=%s /BAUD=%s' _port _baud

; Open serial connection
connect _constr
if result != 2 then
    yesnobox 'Connection fail. Try again?' 'TTY Caution'
    if result then
        goto connect
    else
        goto _end
    endif
endif

; Entering synchronous communication mode (useful in most cases)
setsync 1

; Assigning new title to TeraTerm window
settitle 'COM_BOX'
setmulticastname 'COM_BOX'

; Start here
:main_loop
waitln make_msg
sendmulticast 'DEV_MACHINE' make_msg 13 10

; Minimum delay time between each build
pause 5

; Loop for the new make process
goto main_loop

:_end
unlink
end

; *****************************************************
; *
; *   Function        : get_com_info
; *   Description     : The the COM connection information
; *   Input           : None
; *   Output          : ret (0: Not Found, 1: Found)
; *                     _port (string contains the COM port)
; *                     _baud (string contains the COM baud rate)
; *
; *****************************************************
:get_com_info
ret = 0
filesearch conf_file
if result=1 then
    fileopen fhandle conf_file 0 1
    filestrseek fhandle '[COM]'#13#10
    if result=1 then
        filereadln fhandle _buffer
        strsplit _buffer '='
        _port = groupmatchstr2
        filereadln fhandle _buffer
        strsplit _buffer '='
        _baud = groupmatchstr2
        ret = 1
    endif
    fileclose fhandle
endif

return
