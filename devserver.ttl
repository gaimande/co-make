; *****************************************************
; *   SSH connection controller at development server
; *   Version 1.0
; *   Email: gaimande@gmail.com
; *   Date: September 20th 2014
; *****************************************************
include 'config.ttl'

; Hide MACRO.
show -1

:login
call get_authen_info
if ret=0 then
    yesnobox 'Cannot get authentication information. Input again?' 'Dev Caution'
    if result then
        include 'inputssh.ttl'
        if ret=0 goto end
        goto login
    else
        end
    endif
endif

; Building connection string
sprintf2 _constr '%s /ssh /2 /auth=password /user=%s /passwd=%s /timeout=10 /F="%s"' _host _user _passwd window_config

:connect
; Openning SSH conection
connect _constr
if result != 2 then
    yesnobox 'Connection fail. Try again?' 'Dev Caution'
    if result then
        goto login
    else
        goto _end
    endif
endif

; Wait for connection is establised successfully
call wait_connect

; Set the shell prompt as an clue to detect
mpause 100
call set_prompt

; Restore Tera Term.
showtt 1

; Entering synchronous communication mode (useful in most cases)
setsync 1

; Clear screen and ready to use
mpause 100
clearscreen 0
send #13

; Assigning new title to TeraTerm window
settitle 'DEV_MACHINE'
setmulticastname 'DEV_MACHINE'

:main_loop
; Wait for user enter make command
call wait_make_cmd
sendmulticast 'COM_BOX' ack_msg 13 10
mpause 100
sendmulticast 'COM_BOX' ack_msg 13 10

:waiting
; Wail till finish or any errors occur
; Support make a clue when nearly done by speed up LED blinking
timeout = 60
strlen finish_stage_msg
if result > 0 then
    waitln prompt 'Error 1' #3 finish_stage_msg
else
    waitln prompt 'Error 1' #3
endif    

; Finish make process. Need to check whether success or not
if result=0 then
    flushrecv
    goto waiting
elseif result=1 then
    call check_result
    if ret=1 then
        call display_success
    else
        call display_error
    endif
elseif result=2 then
    ; Send CTRL+C to halt the making process
	sendln #3
    call display_error
elseif result=3 then
	call display_error
elseif result=4 then
    sendmulticast 'COM_BOX' 'FINISH_STAGE' 13 10
    mpause 100
    sendmulticast 'COM_BOX' 'FINISH_STAGE' 13 10
    goto waiting
endif

mpause 200
sendmulticast 'COM_BOX' done_msg 13 10
timeout = 0

; Loop for the new make process
goto main_loop

:_end
unlink
end

; *****************************************************
; *
; *   Function        : set_prompt
; *   Description     : Set prompt for build server
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:set_prompt
strlen prompt
if result=0 then
    prompt = 'you@co-make'
    sprintf2 dftPrompt "PS1='\[\e[1;32m\]%s ~$ '" prompt
    sendln dftPrompt 13 10
endif

return


; *****************************************************
; *
; *   Function        : check_result
; *   Description     : Check the make result after making
; *   Input           : None
; *   Output          : ret (0: error, 1: OK)
; *
; *****************************************************
:check_result

sendln 'echo $?'

flushrecv
waitn 10

strmatch inputstr '0'
if result=0 then
    ret = 0
else
    ret = 1
endif

return


; *****************************************************
; *
; *   Function        : display_error
; *   Description     : Display error pop-up window
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:display_error

exec 'cmd /c start %comspec% /c "mode 40,10&title Making Status&color 4e&echo.&echo. Build Failed.&echo. Please check the output log!&echo.&echo. Press any keys to exit!&pause>NUL"'

; Send status to TTY Controller
sendmulticast 'COM_BOX' 'ERROR' 13 10
mpause 100
sendmulticast 'COM_BOX' 'ERROR' 13 10

return


; *****************************************************
; *
; *   Function        : display_success
; *   Description     : Display success pop-up window
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:display_success

exec 'cmd /c start %comspec% /c "mode 40,10&title Making Status&color 2e&echo.&echo. Build Successfully.&echo.&echo. Press any keys to exit!&pause>NUL"'

; Send status to TTY Controller
sendmulticast 'COM_BOX' 'SUCCESS' 13 10
mpause 100
sendmulticast 'COM_BOX' 'SUCCESS' 13 10

return


; *****************************************************
; *
; *   Function        : get_authen_info
; *   Description     : The the authentication information
; *   Input           : None
; *   Output          : ret (0: Not Found, 1: Found)
; *                     _host (string contains the host's IP address
; *                     _user (string contains the username)
; *                     _passwd (string contains the password)
; *
; *****************************************************
:get_authen_info
ret = 0
filesearch conf_file
if result=1 then
    fileopen fhandle conf_file 0 1
    filestrseek fhandle '[Password]'#13#10
    if result=1 then
        filereadln fhandle _buffer
        strsplit _buffer '='
        _user = groupmatchstr1
        getpassword conf_file _user _passwd
        if result=1 then
            filereadln fhandle _buffer
            strsplit _buffer '='
            _host = groupmatchstr2
            ret = 1
        endif
    endif
    fileclose fhandle
endif

return


; *****************************************************
; *
; *   Function        : wait_make_cmd
; *   Description     : Wait till we receive the 'make' command
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:wait_make_cmd
; Update user's modification at running time
include 'config.ttl'

until result=1
    flushrecv
    recvln
    strmatch inputstr make_msg
enduntil

return


; *****************************************************
; *
; *   Function        : wait_connect
; *   Description     : Wait till connection is establised
; *   Input           : None
; *   Output          : None
; *
; *****************************************************
:wait_connect
until result=1    
	sendln 'echo Welcome to co-make!'
	timeout = 1
    waitln 'Welcome to co-make!'
enduntil
timeout = 0

return