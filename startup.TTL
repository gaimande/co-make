; *****************************************************
; *   Auto halt the making process when any error occur
; *   Version 1.0
; *   Author: quyenlv@dasannetworks.com
; *   Date: September 20th 2014
; *****************************************************
include 'F:\boxConfig.ttl'

; Hide MACRO dialog box
show -1

:login_ssh
; Calling Popup window to get host address
inputbox 'Enter Host IP address' 'Login'
_hostaddr=inputstr
; Check NULL
strcompare _hostaddr ""
if result=0 goto login_ssh 

; Calling Popup window to get username
:get_user
inputbox 'Enter username' 'Login'
_user=inputstr
; Check NULL
strcompare _user ""
if result=0 goto get_user

; Calling Popup window to get password
:get_password
passwordbox 'Enter password' 'Login'
_passwd=inputstr
; Check NULLs
strcompare _passwd ""
if result=0 goto get_password

; Building connection string
sprintf2 _constr_ssh '%s /ssh /2 /auth=password /user=%s /passwd=%s /timeout=7' _hostaddr _user _passwd

:connect_ssh
; Openning SSH conection
statusbox  '\n   Trying to connect thorugh SSH. Please wait...   \n ' 'Notice' 1
connect _constr_ssh
closesbox
if result != 2 then
   yesnobox 'Connection fail. Try again?' 'Caution'
	if result then
	   goto login_ssh
	else
		end
	endif
endif

; The timeout limit is 10 sec.
timeout = 10

; Building prompt string from user name
sprintf2 prompt '%s@' _user
wait prompt
if result=0 then
   yesnobox 'Connection fail. Try again?' 'Caution'
	if result then
	   goto login_ssh
	else
		end
	endif
endif

timeout = 0

; Save SSH authentication information
fileopen fhandle auth_file 0
filewrite fhandle _constr_ssh
fileclose fhandle
	      
closett

:login_tty
; Calling Popup window to get host address
inputbox 'Enter COM port number' 'Connect to serial port'
_comport=inputstr
; Check NULL
strcompare _comport ""
if result=0 goto login_tty

; Check numeric
str2int val _comport
if result=0 goto login_tty

; Building connection string
sprintf2 _constr_tty '/C=%s /BAUD=9600' _comport

:connect_tty
; Openning serial conection
connect _constr_tty
if result != 2 then
   yesnobox 'Connection fail. Try again?' 'Caution'
	if result then
	   goto login_tty
	else
		goto _end
	endif
endif

fileopen fhandle com_file 0
filewrite fhandle _constr_tty
fileclose fhandle

; Start dev65.ttl script
getttdir dir_tearterm
sprintf2 cmd_str 'cmd /c start "" "%s\TTERMPRO" /M=F:\dev65.ttl /F=F:\WINDOW.ini' dir_tearterm
exec cmd_str

; Start ttyBox.ttl script
sprintf2 cmd_str 'cmd /c start "" "%s\TTERMPRO" /M=F:\TTY_BOX.ttl /F=F:\WINDOW.ini /V' dir_tearterm
exec cmd_str

:_end
closett
end