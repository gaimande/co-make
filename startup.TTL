; *****************************************************
; *   Auto halt the making process when any error occur
; *   Version 1.0
; *   Author: quyenlv@dasannetworks.com
; *   Date: September 20th 2014
; *****************************************************
include 'E:\sBox\sBox\boxConfig.ttl'

; Hide MACRO dialog box
;show -1

call CheckSavedConf
if ret = 1 then
    yesnobox 'Saved configuration found!. Do you want to use it?' 'Information'
    if result then
        goto let_start
    else
        filedelete auth_file
        filedelete com_file
    endif
endif

include 'inputSSH.TTL'
if ret=0 goto _end

include 'inputCOM.TTL'
if ret=0 goto _end

:let_start

getttdir dir_tearterm

; Start ttyBox.ttl script
sprintf2 cmd_str 'cmd /c start "" "%s\TTERMPRO" /M=%s' dir_tearterm macro_tty_box
exec cmd_str

; Start dev65.ttl script
sprintf2 cmd_str 'cmd /c start "" "%s\TTERMPRO" /M=%s' dir_tearterm macro_dev_server
exec cmd_str

:_end
end

; *****************************************************
; *
; *   Function      : CountDown
; *   Description   : Display countdown message
; *   Input         : cnt - time to count in second
; *                   cnt_title - title of countdown box
; *                   cnt_msg - message display
; *   Output        : result (0-No key pressed ; 1-Key pressed)
; *
; *****************************************************
:CountDown
    while cnt>=0
        sprintf2 cnt_str '\n%s %d seconds\n ' cnt_msg cnt
        statusbox cnt_str cnt_title 1
		
        cnt = cnt - 1
		
        pause 1 
    endwhile
return



; *****************************************************
; *
; *   Function      : CheckSavedConf
; *   Description   : Check the existence of saved configuration
; *   Input         : None
; *   Output        : ret (0-Not Found ; 1-Found)
; *
; *****************************************************
:CheckSavedConf
ret = 0
filesearch auth_file
if result=1 then
    filesearch com_file
    if result=1 then
	    ret = 1
    endif
endif

return